plugins {
    id 'java-library'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.4.4'
    id 'org.sonarqube' version '3.0'
}

apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/spotbugs.gradle'
apply from: 'gradle/sonarqube.gradle'

repositories {
        jcenter()
}

dependencies {
    implementation 'com.beust:jcommander:1.71'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}

group ''
version '0.1.0'

java {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
}

application {
    mainClassName "${project.group}.katafy.Main"
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClassName,
        )
    }
}

test {
    // use JUnit5
    useJUnitPlatform()
}

task quality {
    dependsOn coverage
    dependsOn bugs
    dependsOn style
}

// Make adjustments to 'check' task to depend on quality as a whole
check.setDependsOn(check.taskDependencies.getDependencies() - checkstyleMain - checkstyleTest)
check.setDependsOn(check.taskDependencies.getDependencies() - spotbugsMain - spotbugsTest)
check.dependsOn quality
